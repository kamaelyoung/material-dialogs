/*
 * Copyright (c) 2020. Beijing EEO Education Ltd. All Rights Reserved.
 */

/**
 * 公司内部 maven库 发布脚本
 *
 * Usage :
 *
 * 在要发布的模块build.gradle文件中底部加入如下内容：
 *
 * ext {
 *   GROUPID = MAVEN_GROUP_ID   // 组名
 *   ARTIFACTID = DARKELF_ARTIFACTID  // 模块名
 *   VERSION = DARKELF_PUBLISH_VERSION  // 模块版本  后缀 -SNAPSHOT 区分是否快照版本
 * }
 * apply from: "https://gl.eeo.im/eeo-android/doc/raw/master/maven_publish.gradle"
 * apply from: rootProject.file("maven_publish.gradle")
 **/

apply plugin: 'maven-publish'

def isAndroid = plugins.hasPlugin('com.android.library')

ext {
  maven_repo_release = "http://10.0.0.193:8081/repository/maven-releases/"
  maven_repo_snapshot = "http://10.0.0.193:8081/repository/maven-snapshots/"
  maven_name = "publisher"
  maven_token = "eeod1vd!v"
  isReleaseVersion = !(VERSION =~ /-SNAPSHOT$/)
}
afterEvaluate { project ->
  tasks.all { Task task ->
    if (task.name.equalsIgnoreCase('generatePomFileForMavenLibraryPublication')) {
      task.dependsOn tasks.getByName('assembleRelease')
    }
  }
}
// 打包源代码
task sourseJar(type: Jar) {
  classifier = 'sources'
  if (isAndroid) {
    from android.sourceSets.main.java.srcDirs
  } else {
    from sourceSets.main.allSource
  }
}

publishing {
  publications {
    mavenLibrary(MavenPublication) {
      groupId GROUPID
      artifactId ARTIFACTID
      version VERSION

      if (isAndroid) {
        artifact "$buildDir/outputs/aar/${project.getName()}-release.aar"
      } else {
        from components.java
      }
      artifact sourseJar
      
      // 添加依赖项，加入该模块依赖的第三方库
      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')
        configurations.implementation.allDependencies.withType(ModuleDependency) {ModuleDependency dp ->
          def dependencyNode = dependenciesNode.appendNode('dependency')
          dependencyNode.appendNode('groupId', dp.group)
          dependencyNode.appendNode('artifactId', dp.name)
          dependencyNode.appendNode('version', dp.version)
          if (dp.excludeRules.size() > 0) {
            def exclusions = dependencyNode.appendNode('exclusions')
            dp.excludeRules.each { ExcludeRule ex ->
              def exclusion = exclusions.appendNode('exclusion')
              exclusion.appendNode('groupId', ex.group)
              exclusion.appendNode('artifactId', ex.module)
            }
          }
        }
      }
     
      
    }
  }

  repositories {
    maven {
      url isReleaseVersion ? maven_repo_release : maven_repo_snapshot
      credentials {
        username maven_name
        password maven_token
      }
    }
  }
}
